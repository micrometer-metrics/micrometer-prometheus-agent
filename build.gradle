plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.micrometer:micrometer-registry-prometheus:latest.release'

    implementation 'io.javalin:javalin:latest.release'
    implementation 'ch.qos.logback:logback-classic:latest.release'
}

jar {
    manifest {
        attributes(
                'Agent-Class': 'io.micrometer.agent.prometheus.PrometheusAgent',
                'Premain-Class': 'io.micrometer.agent.prometheus.PrometheusAgent'
        )
    }
}

shadowJar {
    configurations = [project.configurations.compileClasspath]
    archiveClassifier = null
    dependencies {
        include(dependency('io.micrometer:'))
        include(dependency('io.prometheus:'))
    }
    relocate 'io.micrometer.core', 'io.micrometer.shaded.micrometer.core'
    relocate 'io.micrometer.prometheus', 'io.micrometer.shaded.micrometer.prometheus'
    relocate 'io.prometheus', 'io.micrometer.shaded.prometheus'
}

jar.enabled = false
jar.dependsOn shadowJar
publishing {
    publications {
        withType(MavenPublication) {
            artifact(shadowJar)

            pom.withXml {
                asNode()
                        .dependencies
                        .dependency
                        .findAll {
                            ['micromter-registry-prometheus', 'micrometer-core', 'simpleclient_common'].contains(it.artifactId.text())
                        }
                        .each { it.parent().remove(it) }
            }
        }
    }
}
